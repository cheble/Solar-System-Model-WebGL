<!DOCTYPE html>
<html>
<head>
    <title>CSCE 470 - Solar System</title>
<meta http-equiv="Content-Type" content="text/html;charset=utf-8">	

<!-- Orbits related vertex and fragment shaders -->
<script id="orbit-vertex-shader" type="x-shader/x-vertex">
attribute vec4 vPosition;

uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform float orbitSize;

void
main()
{    
	gl_PointSize = orbitSize;
    gl_Position = projectionMatrix * modelViewMatrix * vPosition;
}
</script>

<script id="orbit-fragment-shader" type="x-shader/x-fragment">
precision mediump float;

#extension GL_EXT_frag_depth : enable

uniform vec4 orbitColor;

void
main()
{
	gl_FragColor = orbitColor;
	// Move the orbit points just behind the planets in the z buffer
	// This should make it so you dont see the orbit overtop of the planet
	gl_FragDepthEXT = gl_FragCoord.z + 0.00001;
}
</script>


<!-- Planets related vertex and fragment shaders -->
<script id="sphere-vertex-shader" type="x-shader/x-vertex">
attribute vec4 vPosition;

varying vec3 fPosition;
varying vec3 fCenter;
varying vec3 L, E;

uniform vec4 lightPosition;


precision mediump float;
uniform vec4 center;
uniform float radius;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;

void
main()
{
	vec3 pPrime = (modelViewMatrix * center).xyz;			// point of center after mv transformation
	
	vec3 v = vec3(0.0, 0.0, 0.0) - pPrime;					// vector from center to eye
	
	vec3 vPrime = vec3(-v.y, v.x, 0.0);						// vector orthagonal to v
	if(vPrime == vec3(0.0, 0.0, 0.0))	vPrime = vec3(0.0, v.z, -v.y);
	if(vPrime == vec3(0.0, 0.0, 0.0))	vPrime = vec3(v.z, 0.0, -v.x);
	if(vPrime == vec3(0.0, 0.0, 0.0))	vPrime = vec3(1.0, 0.0, 0.0);
	
	if(vPosition.x == 1.0){
		
		vPrime = cross(v, vPrime);							// orthagonal to v and coplanar to previous vPrime
		
	} else if (vPosition.x == 2.0){
		
		vPrime = cross(v, vPrime);
		vPrime = cross(v, vPrime);							// orthagonal to v and coplanar to previous vPrimes
		
	} else if (vPosition.x == 3.0){
		
		vPrime = cross(v, vPrime);
		vPrime = cross(v, vPrime);
		vPrime = cross(v, vPrime);							// orthagonal to v and coplanar to previous vPrimes
		
	}

	vec3 t = pPrime + normalize(vPrime)*radius*sqrt(2.0);// vertex position of the square
	
	vec4 tPrime = projectionMatrix * vec4(t, 1.0);			// vertex position with the projection applied

    gl_Position = tPrime;
	
	fPosition = t;											// vertex position without projection
	fCenter = pPrime;										// center position without projection
    
	E = normalize( - (tPrime).xyz);							// vertex from the position to the eye
    L = normalize((projectionMatrix * modelViewMatrix * lightPosition) - tPrime).xyz;	// vetex position of the light
}
</script>

<script id="sphere-fragment-shader" type="x-shader/x-fragment">
precision mediump float;

#extension GL_EXT_frag_depth : enable

varying vec3 fPosition;
varying vec3 fCenter;
varying vec3 L, E;

uniform float radius;
uniform float ka;
uniform float kd;
uniform float ks;
uniform float shininess;
uniform float colorCode;
uniform mat4 invMV;
uniform bool useLighting;
uniform sampler2D texture;

void
main()
{
	gl_FragDepthEXT = gl_FragCoord.z;
	
    vec3 dif = fPosition - fCenter;
	float distance = length(dif);							// distance from the center of the circle
    if (distance > radius) {
		discard;
        return;
	}
	
	if(colorCode != 0.0){
		gl_FragColor = vec4(colorCode, 0.0, 0.0, 1.0);
		return;
	}

    float x = fPosition.x - fCenter.x;
    float y = fPosition.y - fCenter.y;
    float z = sqrt(radius*radius - x * x - y * y);
	
    vec3 norm = normalize(vec3(x, y, -z));					// normal vector of the circle
	
	/* texture parameters */
    float pi = radians(180.0);
	
	// Convert fragment position to object coordinates
	vec3 texVec = ( invMV * vec4(fCenter + vec3(x, y, z), 1.0)).xyz;
	// Convert sphere center positionn to object coordinates
	vec3 tempCenter = (invMV * vec4(fCenter, 1.0)).xyz;
	// Vector from sphere surface position to sphere center
	texVec = texVec - tempCenter; 
	texVec = normalize(texVec);

	// Calculate the s and t coordinates for texture mapping
    float sCoord = 0.5 + (atan(texVec.x,texVec.z))/(2.0*pi);
    float tCoord = 0.5 - asin(texVec.y)/(pi);
    vec2 texCoord = vec2(sCoord, tCoord);

	vec4 fColor = texture2D( texture,  texCoord); 
	/*********************/

	if(!useLighting){
		gl_FragColor = fColor;
		return;
	}
	
	vec3 LL = normalize(L);
	vec3 EE = normalize(E);
    
    vec3 H = normalize( LL + EE );
    float ambient = ka;

    float diffuse = kd * max( dot(LL, norm), 0.0 );
    float specular = ks * pow( max(dot(norm, H), 0.0), shininess );
    
    if( dot(LL, norm) < 0.0 ) specular = 0.0;
	
    fColor = fColor * (ambient + diffuse) + vec4(1.0) * specular;
    fColor.a = 1.0;
    
    gl_FragColor = fColor;
	
}
</script>

<script id="earth-fragment-shader" type="x-shader/x-fragment">
precision mediump float;

#extension GL_EXT_frag_depth : enable

varying vec3 fPosition;
varying vec3 fCenter;
varying vec3 L, E;

uniform float radius;
uniform float ka;
uniform float kd;
uniform float ks;
uniform float shininess;
uniform float colorCode;
uniform mat4 invMV;
uniform bool renderClouds;
uniform sampler2D texture;
uniform sampler2D daycolormap;
uniform sampler2D nightcolormap;
uniform sampler2D specularmap;
uniform sampler2D bumpmap;
uniform sampler2D cloudmap;

void
main()
{
	gl_FragDepthEXT = gl_FragCoord.z;
	
    vec3 dif = fPosition - fCenter;
	float distance = length(dif);							// distance from the center of the circle
    if (distance > radius) {
		discard;
        return;
	}
	
	if(colorCode != 0.0){
		gl_FragColor = vec4(colorCode, 0.0, 0.0, 1.0);
		return;
	}

    float x = fPosition.x - fCenter.x;
    float y = fPosition.y - fCenter.y;
    float z = sqrt(radius*radius - x * x - y * y);
	
    vec3 norm = normalize(vec3(x, y, -z));					// normal vector of the circle
	
	/* texture parameters */
    float pi = radians(180.0);
	
	vec3 texVec = ( invMV * vec4(fCenter + vec3(x, y, z), 1.0)).xyz;
	vec3 tempCenter = (invMV * vec4(fCenter, 1.0)).xyz;
	texVec = texVec - tempCenter; 
	texVec = normalize(texVec);

    float sCoord = 0.5 + (atan(texVec.x,texVec.z))/(2.0*pi);

    float tCoord = 0.5 - asin(texVec.y)/(pi);

    vec2 texCoord = vec2(sCoord, tCoord);
	
	vec4 fColor = texture2D(daycolormap,  texCoord); 
	/*********************/

	vec3 LL = normalize(L);
	vec3 EE = normalize(E);
    
    vec3 H = normalize( LL + EE );
    float ambient = ka;

    float daynightcos = max( dot(LL, norm), 0.0 );  // 1.0 at the equator, 0.0 at the poles
    float diffuse = kd * daynightcos; 
    
    float specMapValue = (texture2D(specularmap, texCoord)).x;
    //  with a better specularMap, these lines aren't necessary
    if(specMapValue > 0.8) specMapValue = 1.0;
    else if(specMapValue < 0.2) specMapValue = 0.0;
    float specular = ks * pow( max(dot(norm, H), 0.0), shininess ) * specMapValue;
    
    if( dot(LL, norm) < 0.0 ) specular = 0.0;
	
	if(renderClouds) {
		gl_FragDepthEXT = gl_FragCoord.z - 0.0001;
		// squared daynightcos for aesthetics
		gl_FragColor = daynightcos*daynightcos * texture2D(cloudmap, texCoord); 
		return;
	}

    fColor = (1.0 - daynightcos)*texture2D(nightcolormap, texCoord) + daynightcos*texture2D(daycolormap, texCoord);

    fColor = fColor * (ambient + diffuse) + vec4(1.0) * specular;
    fColor.a = 1.0;
    
    gl_FragColor = fColor;
	
}
</script>

<!-- Skybox related vertex and fragment shaders -->
<script id="skybox-vertex-shader" type="x-shader/x-vertex">
attribute vec4 vPosition;

uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;

varying vec3 fTexCoord;

void
main()
{    
     gl_Position = projectionMatrix * modelViewMatrix * vPosition;
     fTexCoord = vPosition.xyz;
}
</script>

<script id="skybox-fragment-shader" type="x-shader/x-fragment">
precision mediump float;

varying vec3 fTexCoord;

uniform samplerCube skybox;

void
main()
{
	gl_FragColor = textureCube(skybox, fTexCoord);
}
</script>

<!-- Please, add scripts ordering by name -->
<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="../Util/dat.gui.js"></script>
<script type="text/javascript" src="gui.js"></script>
<script type="text/javascript" src="movement.js"></script>
<script type="text/javascript" src="orbit.js"></script>
<script type="text/javascript" src="planet_data.js"></script>
<script type="text/javascript" src="planet_calc.js"></script>
<script type="text/javascript" src="planet_info.js"></script>
<script type="text/javascript" src="planet_textures.js"></script>
<script type="text/javascript" src="resources.js"></script>
<script type="text/javascript" src="skybox.js"></script>
<script type="text/javascript" src="solar_system.js"></script>

<link rel="stylesheet" href="global.css">
</head>

<body>
<canvas id="gl-canvas" width="2048" height="1024">
Oops ... your browser doesn't support the HTML5 canvas element
</canvas>
<div class="box">
	<div class="infoBox">
		<div class="info">
			<b>Planet Info:</b>
		</div>
		<div class="info" id="info">
			Click on a planet to view information.
		</div>
	</div>
</div>

<div class="box">
	<div class="dateBox">
		<div class="info" id="date">
			
		</div>
		<div class="info" id="dateInputBox">
			<input type="text" id="dateInput" placeholder="YYYY-MM-DD" pattern="[0-9][0-9][0-9][0-9]-[0-1][0-9]-[0-3][0-9]" autocomplete="off">
			<button id="enterDate">Enter</button>
		</div>
	</div>
</div>
</body>
</html>
